        TTL > Bas10 Acronsfot bits - MyOsfile etc

SAVE    ROUT
        JSR     ENDER
        JSR     OSTHIG
        STX     Work+8
        STY     Work+9
        STX     Work+12
        STY     Work+13
        STX     Work+16
        STY     Work+17
        STA     Work+10
        LDX     TOP
        STX     Work+14
        LDX     TOP+1
        STX     Work+15
        LDXIM   0
        STX     Work+6
        LDXIM   &31
        STX     Work+7
        LDX     TXTP
        STX     Work+11
        TAY
        LDXIM   Work
        JSR     MyOsfile
        JMP     NXT

LOAD    ROUT
        JSR     LOADER
        JMP     FSASET

CHAIN   ROUT
        JSR     LOADER
        JMP     RUNNER

LPTR    ROUT
        JSR     AECHAN
        PHA
        JSR     EQEXPR
        JSR     INTEG
        PLA
        TAY
        LDXIM   &2C
        LDAIM   1
        JSR     OSARGS
        JMP     NXT

EXT     ROUT
        SEC
RPTR    LDAIM   0
        ROLA
        ROLA
        PHA
        JSR     CHANN
        LDXIM   &2C
        PLA
        JSR     OSARGS
        LDAIM   &40
        RTS

BPUT    ROUT
        JSR     AECHAN
        PHA
        JSR     COMEAT
        JSR     EXPRDN
        JSR     INTEG
        PLA
        TAY
        LDA     IACC
        JSR     OSBPUT
        JMP     NXT

BGET    ROUT
        JSR     CHANN
        JSR     OSBGET
        JMP     SINSTK

OPENU   ROUT
        LDAIM   &40
        BNE     #01
OPENO   LDAIM   &80
        BNE     #01
OPENI   LDAIM   &C0
01      PHA
        JSR     FACTOR
        BNE     #02
        JSR     OSSTRT
        GENAD   StrAcc
        PLA
        JSR     OsFind
        JMP     SINSTK
02      JMP     LETM

CLOSE   ROUT
        JSR     AECHAN
        JSR     AEDONE
        LDY     IACC
        LDAIM   0
        JSR     OsFind
        JMP     NXT

AECHAN  ROUT
        LDA     CURSOR
        STA     AECUR
        LDA     LINE
        STA     AELINE
        LDA     LINE+1
        STA     AELINE+1
CHANN
        JSR     AESPAC
        CMPIM   "#"
        BNE     MISSING_HASH
        JSR     INTFAC
        LDY     IACC
        TYA
        RTS

VSTRNG  ROUT
        PLA
        STA     Work
        PLA
        STA     Work+1
        LDYIM   0
        BEQ     #02
01      JSR     OSASCI
02      JSR     GETWK2
        CMPIM   0
        BPL     #01
        JMI     Work

REPORT  ROUT
        JSR     DONE
        JSR     NLINE
        LDYIM   1
01      LDAAY   BrkMS
        BEQ     #02
        JSR     TOKOUT
        INY
        BNE     #01
02      JMP     NXT

MISSING_HASH
        BRK
        =       &2D
        =       "Missing #"
        BRK

MyOsfile ROUT
        CMPIM   0
        BEQ     MySave
        CMPIM   &FF
        BEQ     MyLoad
01      JMP     OSFILE

MyLoad  LDA     MOVAD
        BNE     #02
        LDA     MODE
        CMPIM   1
        BEQ     #03
02      LDAIM   &FF
        BNE     #01
03      LDAIM   &40
        JSR     MyFind
04      LDA     Work+2
        STA     Work
        LDA     Work+3
        JSR     SamCvt
        STA     Work+1
        LDXIM   0
        STX     GbpbBlk+5
        INX
        STX     GbpbBlk+6
        LDAIM   4
        JSR     DoGbPb
        LDA     GbpbBlk+6
        BNE     #06
        LDA     GbpbBlk+5
        EORIM   &FF
        TAY
        INY
05      LDAAY   GbBuff
        STAIY   Work
        DEY
        BNE     #05
        LDA     GbBuff
        STAIY   Work
        LDA     GbpbBlk+5
        BNE     #06
        INC     Work+3
        BNE     #04
06      LDY     GbpbBlk
        LDAIM   0
        JMP     OsFind

MySave  LDA     Work+12
        CMPIM   1
        BEQ     #07
        LDAIM   0
        BEQ     #01

07      LDAIM   &80
        JSR     MyFind

08      LDA     Work+11
        CMP     Work+15
        BEQ     #09
        LDYIM   0
        LDXIM   1
        BNE     #11

09      SEC
        LDA     Work+14
        SBC     Work+10
        BEQ     #06
        TAY
        LDA     Work+10
        STA     Work+14
        LDXIM   0
11      STY     GbpbBlk+5
        STX     GbpbBlk+6
        LDA     Work+10
        STA     Work
        LDA     Work+11
        JSR     SamCvt
        STA     Work+1
13      LDAIY   Work
        STAAY   GbBuff
        DEY
        BNE     #13
        LDAIY   Work
        STA     GbBuff
        LDA     GbpbBlk+5
        BNE     #14
        INC     Work+11

14      LDAIM   2
        JSR     DoGbPb
        JMP     #08

DoGbPb  ROUT
        GENAD   GbBuff
        STX     GbpbBlk+1
        STY     GbpbBlk+2
        LDXIM   &FF
        STX     GbpbBlk+3
        STX     GbpbBlk+4
        INX
        STX     GbpbBlk+7
        STX     GbpbBlk+8
        GENAD   GbpbBlk
        JMP     OSGBPB

MyFind  LDX     Work
        LDY     Work+1
        JSR     OsFind
        TAY
        BEQ     #01
        STA     GbpbBlk
        RTS

01      BRK
        =       3
        =       "Not Found"
        BRK

SamCvt  ROUT
        TAX
        LDAAX   OurRam
        STA     &F4
        STA     &FE30
        LDAAX   DAT
        RTS

SamYCvt TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        RTS

TOPcvt  ROUT    ; destroys X and A
        LDA     TOP
        STA     PPTR
        LDA     TOP+1
        JSR     SamCvt
        STA     PPTR+1
        RTS

PhaTs   ROUT    ; push byte in A to (AeStkP),Y
        STY     MoveTY
        PHA
        TYA
        CLC
        ADC     AeStkP
        STA     PPTR
        LDA     AeStkP+1
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        PLA
        STAIY   PPTR
        LDY     MoveTY
        RTS

PlaFs   ROUT    ; pop byte into A from (AeStkP),Y
        STY     MoveTY
        TYA
        CLC
        ADC     AeStkP
        STA     PPTR
        LDA     AeStkP+1
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        LDAIY   PPTR
        LDY     MoveTY
        RTS

LWrkIY  ROUT
        LDA     UpDown
        BEQ     #01
LWUpIY  STY     MoveTY
        TYA
        CLC
        ADC     Work
        STA     PPTR
        LDA     Work+1
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        LDAIY   PPTR
        LDY     MoveTY
        RTS
01      LDAIY   Work
        RTS

LLinIY  ROUT
        LDA     UpDown
        BEQ     #01
LLUpIY  STY     MoveTY
        TYA
        CLC
        ADC     LINE
        STA     PPTR
        LDA     LINE+1
Lcom    ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        LDAIY   PPTR
        LDY     MoveTY
        RTS
01      LDAIY   LINE
        RTS

LWp4UpIY ROUT
        STY     MoveTY
        TYA
        CLC
        ADC     MODE
        STA     PPTR
        LDA     CONSTA
        JMP     Lcom

LAeLnIY ROUT
        LDA     UpDown
        BEQ     #01
        STY     MoveTY
        TYA
        CLC
        ADC     AELINE
        STA     PPTR
        LDA     AELINE+1
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        LDAIY   PPTR
        LDY     MoveTY
        RTS
01      LDAIY   AELINE
        RTS

SWrkIY  ROUT
        BIT     UpDown
        BPL     #01
SWUpIY  PHA
        STY     MoveTY
        TYA
        CLC
        ADC     Work
        STA     PPTR
        LDA     Work+1
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        PLA
        STAIY   PPTR
        LDY     MoveTY
        RTS
01      STAIY   Work
        RTS

SWp4UpIY ROUT
        PHA
        STY     MoveTY
        TYA
        CLC
        ADC     MODE
        STA     PPTR
        LDA     CONSTA
        JMP     Scom

LWp5UpIY ROUT
        STY     MoveTY
        TYA
        CLC
        ADC     CONSTA
        STA     PPTR
        LDA     MOVAD
        JMP     Lcom

LILIY   ROUT
        LDA     UpDown
        BEQ     #01
LILUp   STY     MoveTY
        TYA
        CLC
        ADC     IACC
        STA     PPTR
        LDA     IACCM
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        LDAIY   PPTR
        LDY     MoveTY
        STA     MoveTY
        LDA     MoveTY
        RTS
01      LDAIY   IACC
        RTS

SILIY   ROUT
        BIT     UpDown
        BPL     #01
SILUp
        PHA
        STY     MoveTY
        TYA
        CLC
        ADC     IACC
        STA     PPTR
        LDA     IACCM
        ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        PLA
        STAIY   PPTR
        LDY     MoveTY
        RTS
01      STAIY   IACC
        RTS

SFsaIY  ROUT
        PHA
        STY     MoveTY
        TYA
        CLC
        ADC     FSA
        STA     PPTR
        LDA     FSA+1
Scom    ADCIM   0
        TAY
        LDAAY   OurRam
        STA     &F4
        STA     &FE30
        LDAAY   DAT
        STA     PPTR+1
        LDYIM   0
        PLA
        STAIY   PPTR
        LDY     MoveTY
        RTS

        =    "DiswuzhackedaboutbySam&PMFatacro"

        END
